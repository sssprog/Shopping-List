
buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.parse.com/repo' }
    }
    dependencies {
        classpath 'com.parse.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.parse'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.rengwuxian.materialedittext:library:2.1.3'

    compile fileTree(dir: 'libs', include: ['*.jar'])
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.sssprog.shoppingliststandalone"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 18
        versionName "2.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        releaseConfig {
            if (project.hasProperty('storeFile')) {
                println 'Generating a signed package.'
                storeFile = file(project.storeFile)
                keyAlias = project.keyAlias
                storePassword = project.storePassword
                keyPassword = project.keyPassword
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.releaseConfig
            minifyEnabled true
            proguardFile 'proguard-rules.pro'
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }

        debug {
            minifyEnabled false
            proguardFile 'proguard-rules.pro'
            proguardFile getDefaultProguardFile('proguard-android.txt')
            applicationIdSuffix ".debug"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
